plugins {
	id 'java'                                    					  // Java 빌드 기능 제공
	id 'org.springframework.boot' version '3.4.1' apply false  		  // 스프링 부트 빌드 도구
	id 'io.spring.dependency-management' version '1.1.4' apply false  // 의존성 버전 관리
}

// 모든 프로젝트(루트+서브모듈)에 적용
// my-project-0.0.1-SNAPSHOT.jar 형식으로 저장됨
// com/example/my-project/0.0.1-SNAPSHOT/my-project-0.0.1-SNAPSHOT.jar
allprojects {
	group = 'com.example'    	// Maven 그룹 ID(빌드 결과물 식별)
	version = '0.0.1-SNAPSHOT'  // 프로젝트 버전
	repositories {
		mavenCentral()  		// 의존성을 다운로드할 저장소(mavenCentral은 중앙 저장소다.)
	}
}

// 서브모듈에만 적용
subprojects {
	apply plugin: 'java'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	java {
		sourceCompatibility = '21'  // 소스코드 자바 버전
		targetCompatibility = '21'  // 컴파일 대상 자바 버전
	}

	dependencies {  // 모든 서브모듈의 공통 의존성
		compileOnly 'org.projectlombok:lombok'          // 버전 명시
		annotationProcessor 'org.projectlombok:lombok'  // 버전 명시

		// 테스트 의존성 추가
		testImplementation 'org.springframework.boot:spring-boot-starter-test'  // JUnit 포함
	}

	test {
		useJUnitPlatform()
	}
}